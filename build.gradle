plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

ext {
    project_name = 'Liberium'
    project_group = 'com.kraftics'
    project_version_build = System.properties.getProperty("buildnum") as Integer
    project_version = "1.1.1+v2" + (project_version_build == null ? "" : "." + project_version_build.toString())
    project_description = 'Spigot library to make plugin coding fun and easier'
    project_url = 'https://kraftics.com/liberium'
    project_jdk = '11'

    isRelease = !project_version.toString().contains('beta')
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    group = project_group
    version = project_version
    description = project_description

    sourceCompatibility = project_jdk
    targetCompatibility = project_jdk

    repositories {
        mavenCentral()
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    }

    dependencies {
        compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'

        implementation 'org.jetbrains:annotations:23.0.0'

        testImplementation 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    }

    test {
        useJUnitPlatform()

        ignoreFailures true
    }
}

subprojects {
    archivesBaseName = "liberium-$project.name"

    java {
        withSourcesJar()
        withJavadocJar()
    }

    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId = archivesBaseName
                groupId = project_group
                version = project_version

                pom {
                    name = project_name
                    description = project_description
                    url = 'https://github.com/KrafticsTeam/Liberium'

                    organization {
                        name = 'KrafticsTeam'
                        url = 'https://kraftics.com'
                    }

                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/KrafticsTeam/Liberium/blob/master/LICENSE'
                            distribution = 'repo'
                        }
                    }

                    scm {
                        url = 'https://github.com/KrafticsTeam/Liberium'
                        connection = 'scm:git:git://github.com/KrafticsTeam/Liberium.git'
                        developerConnection = 'scm:git:ssh://git@github.com:KrafticsTeam/Liberium.git'
                    }

                    developers {
                        developer {
                            name = 'KrafticsTeam'
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                if (isRelease) {
                    url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                } else {
                    url 'https://oss.sonatype.org/content/repositories/snapshots/'
                }

                def sonatypeUsername = findProperty('sonatypeUsername')
                def sonatypePassword = findProperty('sonatypePassword')

                if (sonatypeUsername != null && sonatypePassword != null) {
                    credentials {
                        username = sonatypeUsername
                        password = sonatypePassword
                    }
                }
            }
        }
    }

    if (isRelease) {
        signing {
            useGpgCmd()
            sign publishing.publications.mavenJava
        }
    }
}

jar {
    onlyIf { false }
}